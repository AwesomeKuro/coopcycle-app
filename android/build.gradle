// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 21
        compileSdkVersion = 33
        targetSdkVersion = 33
        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
        playServicesVersion = "18.1.0"
        firebaseVersion = "17.2.1"
        timberVersion = "4.7.1"
        kotlinVersion = "1.6.10"
        androidXCore = "1.0.2"
        // https://github.com/react-native-maps/react-native-maps/issues/4842
        googlePlayServicesLocationVersion = "20.0.0"
        appCompatVersion = "1.4.2"
        mercadoPagoPxVersion = "4.53.2"
        reactNativeVersion = "0.70.6" // https://github.com/expo/expo/issues/18129
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.3.1")
        classpath("com.facebook.react:react-native-gradle-plugin")

        classpath("com.google.gms:google-services:4.3.8")
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        maven {
            // react-native-background-fetch
            url("${project(':react-native-background-fetch').projectDir}/libs")
        }
        if (findProject(':react-native-background-geolocation') != null) {
          maven {
              // Required for react-native-background-geolocation
              url("${project(':react-native-background-geolocation').projectDir}/libs")
          }
        }
        maven {
          url 'https://artifacts.mercadolibre.com/repository/android-releases/'
        }
        maven {
            // expo-camera bundles a custom com.google.android:cameraview
            url "$rootDir/../node_modules/expo-camera/android/maven"
        }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
                compileSdkVersion rootProject.ext.compileSdkVersion
            }
        }
    }
}
